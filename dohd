#!/usr/bin/perl
# Simple DNS over HTTPS server. Copyright 2018 CentralNic Ltd.
use File::Basename qw(basename);
use Getopt::Long;
use HTTP::Daemon;
use List::MoreUtils qw(any);
use MIME::Base64;
use Net::DNS;
use POSIX qw(setsid);
use Pod::Usage;
use Sys::Syslog qw(:standard :macros);
use URI;
use constant CONTENT_TYPE => 'Content-Type';
use constant X_FORWARDED_FOR => 'X-Forwarded-For';
use strict;

#
# acceptable media types for requests
#
my @types = qw(application/dns-message application/dns-udpwireformat);

#
# defaults
#
my $addr	= '127.0.0.1';
my $port	= '8080';
my $raddr	= '127.0.0.1';
my $daemon	= undef;
my $help	= undef;

GetOptions(
	'addr=s'	=> \$addr,
	'port=i'	=> \$port,
	'resolver=s'	=> \$raddr,
	'debug'		=> \$HTTP::Daemon::DEBUG,
	'daemon'	=> \$daemon,
	'help'		=> \$help,
);

pod2usage(1) if ($help);

my $resolver = Net::DNS::Resolver->new(
	'nameservers'		=> [ $raddr ],
	'debug'			=> $HTTP::Daemon::DEBUG,
	'persistent_tcp'	=> 1,
	'tcp_timeout'		=> 1,
	'udp_timeout'		=> 1,
	'retry'			=> 1,
);

openlog(basename(__FILE__), 'pid,perror', LOG_DAEMON);
setlogmask(LOG_UPTO(LOG_DEBUG));

my $server = HTTP::Daemon->new(
	'LocalAddr'	=> $addr,
	'LocalPort'	=> $port,
);

if (!$server) {
	chomp($@);
	syslog(LOG_CRIT, sprintf('Unable to start server on %s:%u: %s', $addr, $port, $@));
	exit(1);

} else {
	syslog(LOG_INFO, sprintf('DoH server running on %s', $server->url));

}

if ($daemon) {
	syslog(LOG_DEBUG, 'daemonizing');
	if (fork() > 0) {
		exit 0;

	} else {
		setsid();
		chdir('/');
		$0 = sprintf('[%s]', basename(__FILE__));

	}
}

#
# listen for connections
#
while (my $connection = $server->accept) {

	#
	# catch errors by using eval { ... }
	#
	eval {
		handle_connection($connection);
		$connection->close;
		undef($connection);
	};

	if ($@) {
		chomp($@);
		syslog(LOG_DEBUG, sprintf(
			'%s: %s',
			$connection->peerhost,
			$@
		));
	}
}

#
# handle a connection
#
sub handle_connection {

	#
	# $connection is a HTTPP:Daemon::ClientConn
	#
	my $connection = shift;

	#
	# $request is a HTTP::Request
	#
	my $request = $connection->get_request;

	if (!$request) {
		syslog(LOG_DEBUG, sprintf(
			'%s 400 (%s)',
			$connection->peerhost,
			$connection->reason
		));

		$connection->send_error(400);

	} else {
		my $peer;
		if ($request->header(X_FORWARDED_FOR)) {
			$peer = sprintf(
				'%s (%s: %s)',
				$connection->peerhost,
				X_FORWARDED_FOR,
				$request->header(X_FORWARDED_FOR)
			);

		} else {
			$peer = $connection->peerhost;

		}

		#
		# DNS query packet data goes here
		#
		my $data;

		if ($request->method eq 'GET') {

			#
			# extract packet data from query string
			#
			my %params = URI->new_abs(
				$request->uri,
				$server->url
			)->query_form;

			$data = decode_base64($params{'dns'} || $params{'body'});

		} elsif ($request->method eq 'POST') {
			if (!any { lc($_) eq lc($request->header(CONTENT_TYPE)) } @types) {
				syslog(LOG_DEBUG, sprintf(
					"%s 415 (type is '%s')",
					$peer,
					$request->header(CONTENT_TYPE)
				));

				$connection->send_error(415);
				return undef;

			} else {
				$data = $request->content;

			}

		} else {
			syslog(LOG_DEBUG, sprintf(
				"%s 405 (method is '%s')",
				$peer,
				$request->method
			));

			$connection->send_error(405);
			return undef;

		}

		#
		# build packet object from data
		#
		my $packet = Net::DNS::Packet->new(\$data);

		if (!$packet) {
			syslog(LOG_DEBUG, sprintf(
				'%s 400 (unable to parse packet data)',
				$peer
			));

			$connection->send_error(400);

		} else {
			#
			# send the packet to the server
			#
			my $response = $resolver->send($packet);

			if (!$response) {
				syslog(LOG_DEBUG, sprintf(
					'%s 504 (%s)',
					$peer,
					$resolver->errorstring
				));

				$connection->send_error(504);

			} else {
				syslog(LOG_DEBUG, sprintf(
					'%s %s/%s/%s %s',
					$peer,
					($response->question)[0]->qname,
					($response->question)[0]->qclass,
					($response->question)[0]->qtype,
					lc($response->header->rcode)
				));

				#
				# send the response back to the client
				#
				$connection->send_status_line;
				$connection->send_header('Server', basename(__FILE__));
				$connection->send_header(CONTENT_TYPE, $types[0]);
				$connection->send_header('Connection', 'close');
				$connection->send_crlf;
				$connection->print($response->data);

			}
		}
	}
}

__END__

=pod

=head1 NAME

C<dohd>, a DNS over HTTPS (DoH) server.

=head1 DESCRIPTION

C<dohd> is a simple DoH server built using L<Net::DNS> and L<HTTP::Daemon>.
It accepts HTTP requests containing DNS queries, forwards them to a DNS server,
and sends the response back to the client.

=head1 SYNOPSIS

	dohd OPTIONS

=head1 OPTIONS

=over

=item * C<--addr=ADDR> - address to listen on, defaults to C<127.0.0.1>.

=item * C<--port> - port to listen on, defaults to C<8080>.

=item * C<--resolver> - DNS server to forward queries to, defaults to C<127.0.0.1>.

=item * C<--debug> - enables debug mode for C<HTTP::Daemon> and C<Net::DNS::Resolver>.

=item * C<--daemon> - daemonise, otherwise, C<dohd> stays in the foreground.

=item * C<--help> - display help.

=back

=head2 Supporting HTTPS and HTTP/2

The DoH specification (L<https://tools.ietf.org/html/draft-ietf-doh-dns-over-https>)
makes support for HTTPS mandatory, and says that you SHOULD support HTTP/2.

This can be achieved fairly easily by using C<nghttpx>,
(L<https://nghttp2.org/documentation/nghttpx.1.html>) as a reverse proxy sitting
in front of C<dohd>:

	nghttpx -b 127.0.0.1,8080 -f 127.0.0.1,4430 server.key server.crt

The above command will accept HTTP/2 connections over HTTPS on C<127.0.0.1>
port C<4430> and forward them as HTTP/1.1 connections to C<127.0.0.1> port
C<8080>.

=head1 COPYRIGHT

Copyright 2018 CentralNic Ltd. All rights reserved.

=head1 LICENSE

Permission to use, copy, modify, and distribute this software and its
documentation for any purpose and without fee is hereby granted,
provided that the above copyright notice appear in all copies and that
both that copyright notice and this permission notice appear in
supporting documentation, and that the name of the author not be used
in advertising or publicity pertaining to distribution of the software
without specific prior written permission.

THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS
OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF
MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT.
IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY
CLAIM, DAMAGES OR OTHER LIABILITY, WHETHER IN AN ACTION OF CONTRACT,
TORT OR OTHERWISE, ARISING FROM, OUT OF OR IN CONNECTION WITH THE
SOFTWARE OR THE USE OR OTHER DEALINGS IN THE SOFTWARE.

=cut