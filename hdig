#!/usr/bin/perl
# Simple DNS-over-HTTPS client. Copyright 2018 CentralNic Ltd
use File::Basename qw(basename);
use Getopt::Long;
use HTTP::Request::Common;
use LWP::UserAgent;
use Mozilla::CA;
use Net::DNS::Parameters;
use Net::DNS;
use Pod::Usage;
use URI;
use constant URL_TEMPLATE => 'https://%s/dns-query';
use constant CONTENT_TYPE => 'application/dns-message';
use strict;

my ($qname, $qtype, $qclass, $url, $debug, $insecure, $help);

#
# dig-like command lines, things can appear in any order
#
# URL can be explicit, if it starts with https:// or http://, or
# if it starts with @, will be constructed, ie @example.com =>
# https://example.com/dns-query
#
while (scalar(@ARGV) > 0) {
	my $param = shift(@ARGV);

	if ($param =~ /^(-h|--help)$/) {
		$help = 1;

	} elsif ($param =~ /^(-d|--debug)$/) {
		$debug = 1;

	} elsif ($param =~ /^(-k|--insecure)$/) {
		$insecure = 1;

	} elsif ($param =~ /^(@|https?:\/\/)(.+)$/) {
		if ($url) {
			print STDERR "Error: multiple URLs provided\n";
			exit;

		} else {
			if ('@' eq $1) {
				$url = sprintf(URL_TEMPLATE, $2);

			} else {
				$url = $param;

			}
		}

	} elsif ($Net::DNS::Parameters::classbyname{$param}) {
		if ($qclass) {
			print STDERR "Error: multiple classes provided\n";
			exit;

		} else {
			$qclass = $param;

		}

	} elsif ($Net::DNS::Parameters::typebyname{$param}) {
		if ($qtype) {
			print STDERR "Error: multiple types provided\n";
			exit;

		} else {
			$qtype = $param;

		}

	} elsif ($qname) {
		print STDERR "Error: multiple query names provided\n";
		exit;

	} else {
		$qname = $param;

	}
}

pod2usage(1) if ($help);

if (!$qname) {
	print STDERR "Error: no query name provided\n";
	exit(1);
}

$url = sprintf(URL_TEMPLATE, (Net::DNS::Resolver->new->nameservers)[0]) if (!$url);

$qname =~ s/\.$//g;
my $packet = Net::DNS::Packet->new($qname.'.', $qtype || 'A', $qclass || 'IN');

my $request = POST($url, 'Content-Type' => CONTENT_TYPE, 'Content' => $packet->data);
$request->header('Accept' => CONTENT_TYPE);

print STDERR $request->as_string if ($debug);

my %options = (
	'agent' => basename(__FILE__),
);

if ($insecure) {
	$options{'ssl_opts'}->{'verify_hostname'} = undef;

} else {
	$options{'ssl_opts'}->{'verify_hostname'} = 1;
	$options{'ssl_opts'}->{'SSL_ca_file'} = Mozilla::CA::SSL_ca_file();
}

my $ua = LWP::UserAgent->new(%options);

my $response = $ua->request($request);

print STDERR $response->as_string if ($debug);

if ($response->is_error || 200 != $response->code) {
	print STDERR $response->status_line."\n";
	exit(1);

} else {
	my $data = $response->content;
	my $answer = Net::DNS::Packet->new(\$data);
	$answer->print;

}

__END__

=head1 NAME

C<hdig> a C<dig>-like client for DNS over HTTPS (DoH).

=head1 DESCRIPTION

C<hdig> is a simple DNS over HTTPS (DoH) client implemented using L<Net::DNS> and L<LWP>.

It will construct a DNS query, send it as an HTTP request to a DoH server, and display the response in human-readable format.

=head1 SYNOPSIS

	hdig OPTIONS

=head1 OPTIONS

C<hdig> accepts similar command-line options to C<dig>, and like C<dig>, they can be provided in any order.

=over

=item * query name. mandatory.

=item * query type, any RR type supported by your version of L<Net::DNS> will work. Defaults to C<A> if unset.

=item * query class, defaults to C<IN>.

=item * URL. This may be either a fully-qualified URL such as L<https://example.com/dns-query> or a string of the form

    @example.com

This will get turned into the HTTPS URL above.

If no URL is provided, then C<hdig> will construct one using the nameserver the system is configured with.

=item * C<--insecure> or C<-k>. Disables SSL certification verification.

=item * C<--debug> or C<-d>. Enables debug mode.

=item * C<--help> or C<-h>. Displays help.

=back

=head2 EXAMPLES

	$ hdig example.com @cloudflare-dns.com

Sends an A query for C<example.com> to Cloudflare's DoH server.

	$ hdig AAAA example.com @cloudflare-dns.com

Sends an AAAA record.

	$ hdig CH TXT id.server @example.com

Sends a Chaosnet TXT query for C<id.server> to L<https://example.com/dns-query>.

=head1 COPYRIGHT

Copyright 2018 CentralNic Ltd. All rights reserved.

=head1 LICENSE

Permission to use, copy, modify, and distribute this software and its
documentation for any purpose and without fee is hereby granted,
provided that the above copyright notice appear in all copies and that
both that copyright notice and this permission notice appear in
supporting documentation, and that the name of the author not be used
in advertising or publicity pertaining to distribution of the software
without specific prior written permission.

THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS
OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF
MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT.
IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY
CLAIM, DAMAGES OR OTHER LIABILITY, WHETHER IN AN ACTION OF CONTRACT,
TORT OR OTHERWISE, ARISING FROM, OUT OF OR IN CONNECTION WITH THE
SOFTWARE OR THE USE OR OTHER DEALINGS IN THE SOFTWARE.

=cut